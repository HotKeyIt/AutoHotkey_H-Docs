<!DOCTYPE HTML>
<html>
<head>
<title>ToolTip Mouse Menu (requires XP/2k/NT) -- by Rajat</title>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>ToolTip Mouse Menu (requires XP/2k/NT) -- by Rajat</h1>

<p>This script displays a popup menu in response to briefly holding down
 the middle mouse button.  Select a menu item by left-clicking it.
 Cancel the menu by left-clicking outside of it.  A recent improvement
 is that the contents of the menu can change depending on which type of
 window is active (Notepad and Word are used as examples here).
</p>
<p><a href="TooltipMouseMenu.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; You can set any title here for the menu:</em>
MenuTitle := "-=-=-=-=-=-=-=-"

<em>; This is how long the mouse button must be held to cause the menu to appear:</em>
UMDelay := 20

#SingleInstance


<em>;___________________________________________
;_____Menu Definitions______________________</em>

<em>; Create / Edit Menu Items here.
; You can't use spaces in keys/values/section names.</em>

<em>; Don't worry about the order, the menu will be sorted.</em>

MenuItems := "Notepad/Calculator/Section 3/Section 4/Section 5"


<em>;___________________________________________
;______Dynamic menuitems here_______________</em>

<em>; Syntax:
;     Dyn# = MenuItem|Window title</em>

Dyn1 := "MS Word|- Microsoft Word"
Dyn2 := "Notepad II|- Notepad"

<em>;___________________________________________</em>

Exit


<em>;___________________________________________
;_____Menu Sections_________________________</em>

<em>; Create / Edit Menu Sections here.</em>

Notepad:
Run, Notepad.exe
Return

Calculator:
Run, Calc
Return

Section3:
MsgBox, You selected 3
Return

Section4:
MsgBox, You selected 4
Return

Section5:
MsgBox, You selected 5
Return

MSWord:
msgbox, this is a dynamic entry (word)
Return

NotepadII:
msgbox, this is a dynamic entry (notepad)
Return


<em>;___________________________________________
;_____Hotkey Section________________________</em>

~MButton::
HowLong := 0
Loop
{
    HowLong++
    Sleep, 10
    if !GetKeyState("MButton", "P")
        Break
}
if HowLong < UMDelay, return


<em>;prepares dynamic menu</em>
DynMenu := ""
Loop
{
    if Dyn%a_index% = "", break

    ppos := InStr(dyn%a_index%, "|")
    item := SubStr(dyn%a_index%, 1, ppos)
    ppos += 2
    win := SubStr(dyn%a_index%, ppos, 1000)

    if WinActive(win)
        DynMenu .= "/" item
}


<em>;Joins sorted main menu and dynamic menu</em>
TempMenu := Sort(MenuItems, "D/") DynMenu


<em>;clears earlier entries</em>
Loop
{
    if MenuItem%a_index% = "", break
    MenuItem%a_index% := ""
}

<em>;creates new entries</em>
Loop, Parse, %TempMenu%, /
{
    MenuItem%a_index% := a_loopfield
}

<em>;creates the menu</em>
Menu := MenuTitle
Loop
{
    if MenuItem%a_index% = "", break
    numItems++
    MenuText := MenuItem%a_index%
    Menu .= "`n" MenuText
}

MouseGetPos, mX, mY
HotKey, ~LButton, MenuClick
HotKey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
HotKey, ~LButton, Off
if !WinActive(MenuTitle)
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <em>;space after which first line starts</em>
mY /= 13    <em>;space taken by each line</em>
if mY < 1, return
if mY > numItems, return
TargetSection := MenuItem%Round(mY)%
StrReplace, TargetSection, %TargetSection%, %a_space%
Gosub, %TargetSection%
Return
</pre>
</body>
</html>
