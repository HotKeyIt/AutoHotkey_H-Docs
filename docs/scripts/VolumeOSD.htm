<!DOCTYPE HTML>
<html>
<head>
<title>Volume On-Screen-Display</title>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>Volume On-Screen-Display <span class="headnote">Based on the v1 script by Rajat</span></h1>

<p>This script assigns hotkeys of your choice to raise and lower the
 master and/or wave volume.  Both volumes are displayed as different
 color bar graphs.
</p>
<p><a href="VolumeOSD.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; --- User Settings ---

; The percentage by which to raise or lower the volume each time:</em>
config := {Step: 4}

<em>; How long to display the volume level bar graphs:</em>
config.DisplayTime := 2000

<em>; Master Volume Bar color (see the help file to use more precise shades):</em>
config.CBM := "Red"

<em>; Wave Volume Bar color:</em>
config.CBW := "Blue"

<em>; Background color:</em>
config.CW := "Silver"

<em>; Bar's screen position. Use "center" to center the bar in that dimension:</em>
config.PosX := "center"
config.PosY := "center"
config.Width := 150  <em>; width of bar</em>
config.Thick := 12   <em>; thickness of bar</em>

<em>; If your keyboard has multimedia buttons for Volume, you can
; try changing the below hotkeys to use them by specifying
; Volume_Up, ^Volume_Up, Volume_Down, and ^Volume_Down:</em>
config.MasterUp := "#Up"      <em>; Win+UpArrow</em>
config.MasterDown := "#Down"
config.WaveUp := "+#Up"       <em>; Shift+Win+UpArrow</em>
config.WaveDown := "+#Down"

<em>; --- Auto Execute Section ---

; DON'T CHANGE ANYTHING HERE (unless you know what you're doing).</em>

#SingleInstance

<em>; Create the Progress window:</em>
G := GuiCreate("+ToolWindow -Caption -Border +Disabled")
G.MarginX := 0, G.MarginY := 0
opt := "w" config.Width " h" config.Thick " background" config.CW
G.Add("Progress", opt " vMaster c" config.CBM)
G.Add("Progress", opt " vWave c" config.CBW)

<em>; Create function references for the hotkeys:</em>
vol_MasterUp := Func("ChangeVolume").bind(config, G, "+")
vol_MasterDown := Func("ChangeVolume").bind(config, G, "-")
vol_WaveUp := Func("ChangeVolume").bind(config, G, "+", "Wave")
vol_WaveDown := Func("ChangeVolume").bind(config, G, "-", "Wave")

<em>; Register hotkeys:</em>
Hotkey(config.MasterUp, vol_MasterUp)
Hotkey(config.MasterDown, vol_MasterDown)
Hotkey(config.WaveUp, vol_WaveUp)
Hotkey(config.WaveDown, vol_WaveDown)

<em>; --- Function Definitions ---</em>

ChangeVolume(config, G, Prefix, ComponentType := "Master")
{
  SoundSet(Prefix config.Step, ComponentType)
  G.Control["Master"].Value := Round(SoundGet("Master"))
  G.Control["Wave"].Value := Round(SoundGet("Wave"))
  G.Show("x" config.PosX " y" config.PosY)
  HideWindow := Func("HideWindow").bind(G)
  SetTimer(HideWindow, -config.DisplayTime)
}

HideWindow(G)
{
  G.Hide()
}</pre>
</body>
</html>
